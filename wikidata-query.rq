PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

CONSTRUCT {
  ?place
    rdf:type ?type ;
    rdfs:label ?label ;
    skos:altLabel ?altLabel ;
    schema:description ?description ;
    foaf:depiction ?flag ;
    wdt:P1332 ?north ;
    wdt:P1333 ?south ;
    wdt:P1334 ?east ;
    wdt:P1335 ?west ;
    wdt:P3896 ?geo .

  ?type rdfs:label ?typeLabel .
}
WHERE {

  ${bindings}

  ${types}

  ${constraints}

  ?place
    wdt:P31 ?type ;
    rdfs:label ?label ;
    schema:description ?description .

  ?type rdfs:label ?typeLabel .

  OPTIONAL { ?place wdt:P41 ?flag . }

  OPTIONAL {
    ?place
      wdt:P1332 ?north ;
      wdt:P1333 ?south ;
      wdt:P1334 ?east ;
      wdt:P1335 ?west .
  }

  OPTIONAL { ?place wdt:P3896 ?geo . }

  OPTIONAL {
    ?place skos:altLabel ?altLabel .
     FILTER (LANG(?altLabel) = "en")
  }

  FILTER (LANG(?typeLabel) = "en")
  FILTER (LANG(?label) = "en")
  FILTER (LANG(?description) = "en")
}
